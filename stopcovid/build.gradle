/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 *
 * Authors
 * ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
 * Created by Lunabee Studio / Date - 2020/04/05 - for the STOP-COVID project
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'shot'

apply from: 'test.gradle'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            packagingOptions {
                exclude 'DebugProbesKt.bin'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()
    kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"

    defaultConfig {
        applicationId "fr.gouv.android.stopcovid"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 74
        versionName "2.0.1"

        testInstrumentationRunner = 'com.lunabeestudio.stopcovid.TestRunner'

        buildConfigField 'String', 'CAPTCHA_API_KEY', '"6LettPsUAAAAAHYaFdRBOilHUgmTMSIPKNZN4D7l"'
        buildConfigField 'String', 'CAPTCHA_URL', '"https://stopcovid.gouv.fr"'
        buildConfigField 'String', 'APP_MAINTENANCE_CERTIFICATE_SHA256', '"sha256/sXQojvwsiyblrpMQIVRXGC5u7AgknzTJm+VIK1kQmD8="'
        buildConfigField 'String', 'APP_MAINTENANCE_URL', '"https://app.stopcovid.gouv.fr/maintenance/info-maintenance-v2.json"'
        buildConfigField 'String', 'INFO_CENTER_FOLDER', '"InfoCenter/"'
        buildConfigField 'String', 'KEY_FIGURES_PATH', '"/infos/key-figures.json"'
        buildConfigField 'String', 'APP_STORE_URL', '"https://play.google.com/store/apps/details?id=fr.gouv.android.stopcovid"'
    }

    lintOptions {
        disable "InvalidPackage",
                "ObsoleteLintCustomCheck",
                "UnsafeExperimentalUsageError",
                "UnsafeExperimentalUsageWarning"
    }

    testOptions {
        animationsDisabled true
    }

    buildFeatures.viewBinding = true
}

dependencies {
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.4.0'

    implementation 'com.airbnb.android:lottie:3.4.4'

    implementation('com.journeyapps:zxing-android-embedded:4.1.0') { transitive = false }
    //noinspection NewerVersionAvailable,GradleDependency (New version require minSdk 24)
    implementation 'com.google.zxing:core:3.3.0'

    implementation 'com.github.razir.progressbutton:progressbutton:2.1.0'

    implementation project(path: ':coreui')
    implementation project(path: ':framework')
    implementation project(path: ':robert')
    implementation project(path: ':domain')

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
}
