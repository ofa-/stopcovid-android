/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 *
 * Authors
 * ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
 * Created by Lunabee Studio / Date - 2020/04/05 - for the TOUS-ANTI-COVID project
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.protobuf'

android {
    signingConfigs {
        release {
            // You need to specify either an absolute path or include the
            // keystore file in the same directory as the build.gradle file.
            storeFile file("my-release-key.jks")
            storePassword "password"
            keyAlias "my-alias"
            keyPassword "password"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            packagingOptions {
                exclude 'DebugProbesKt.bin'
                pickFirst 'META-INF/AL2.0'
                pickFirst 'META-INF/LGPL2.1'
                pickFirst 'META-INF/ASL-2.0.txt'
                pickFirst 'META-INF/LGPL-3.0.txt'
                pickFirst 'draftv3/schema'
                pickFirst 'draftv4/schema'
            }
            signingConfig signingConfigs.release
        }
    }

    kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    compileOptions {
        coreLibraryDesugaringEnabled = true
    }

    // minSdkVersion & targetSdkVersion
    // duped below from ../build.gradle
    // to avoid conflict on upstream merges
    defaultConfig {
        applicationId "fr.gouv.android.stopcovid"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 428
        versionName "4.0.1"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        animationsDisabled true
    }

    buildFeatures.viewBinding = true

    def props = new Properties()
    file("../versions.properties").withInputStream { props.load(it) }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:${props.getProperty("version.com.google.protobuf..protobuf-javalite")}"
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    java {
                        option "lite"
                    }
                }
            }
        }
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:_"

    implementation "com.github.PhilJay:MPAndroidChart:_"

    implementation "androidx.documentfile:documentfile:_"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:_"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:_"
    implementation "androidx.work:work-runtime-ktx:_"

    implementation "com.airbnb.android:lottie:_"

    implementation("com.journeyapps:zxing-android-embedded:_") { transitive = false }
    implementation "com.google.zxing:core:3.3.3" // 3.4 require minSdk 24

    implementation "com.github.razir.progressbutton:progressbutton:_"

    implementation 'com.github.bastienpaulfr:Treessence:_'

    implementation "com.github.chrisbanes:photoview:_"

    implementation "nl.dionsegijn:konfetti:_"

    implementation 'com.google.protobuf:protobuf-javalite:_'

    implementation 'com.tom-roush:pdfbox-android:_'

    implementation project(path: ':analytics')
    implementation project(path: ':coreui')
    implementation project(path: ':framework')
    implementation project(path: ':robert')
    implementation project(path: ':domain')
    implementation project(path: ':dgca-decoder')

    testImplementation "junit:junit:_"
    testImplementation "io.mockk:mockk:_"

    androidTestImplementation "androidx.test.ext:junit:_"
    androidTestImplementation "androidx.test:runner:_"
    androidTestImplementation 'androidx.test:rules:_'
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:_"
    androidTestImplementation "io.mockk:mockk-android:_"
}

configurations {
    implementation.exclude module:'gauva-jdk5'
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}
