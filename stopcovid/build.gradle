/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 *
 * Authors
 * ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
 * Created by Lunabee Studio / Date - 2020/04/05 - for the TOUS-ANTI-COVID project
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    signingConfigs {
        release {
            // You need to specify either an absolute path or include the
            // keystore file in the same directory as the build.gradle file.
            storeFile file("my-release-key.jks")
            storePassword "password"
            keyAlias "my-alias"
            keyPassword "password"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            packagingOptions {
                exclude 'DebugProbesKt.bin'
                exclude 'META-INF/AL2.0'
                exclude 'META-INF/LGPL2.1'
            }
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()
    kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"

    // minSdkVersion & targetSdkVersion
    // duped below from ../build.gradle
    // to avoid conflict on upstream merges
    defaultConfig {
        applicationId "fr.gouv.android.stopcovid"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 172
        versionName "2.2.7"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'APP_MAINTENANCE_CERTIFICATE_SHA256', '"sha256/sXQojvwsiyblrpMQIVRXGC5u7AgknzTJm+VIK1kQmD8="'
        buildConfigField 'String', 'APP_MAINTENANCE_URL', '"https://app.stopcovid.gouv.fr/maintenance/info-maintenance-v2.json"'

        def gitRev = "git rev-parse HEAD".execute().getText().substring(0, 6)

        buildConfigField 'String', 'BUILD_ID', /"${gitRev}"/

        buildConfigField 'String', 'INFO_CENTER_FOLDER', '"InfoCenter/"'
        buildConfigField 'String', 'MORE_KEY_FIGURES_FOLDER', '"MoreKeyFigures/"'
        buildConfigField 'String', 'KEY_FIGURES_PATH', '"/infos/key-figures.json"'
        buildConfigField 'String', 'FORM_PATH', '"/Attestations/form.json"'
        buildConfigField 'String', 'VACCINATION_CENTER_PATH', '"/infos/dep/"'
        buildConfigField 'String', 'VACCINATION_CENTER_FILENAME', '"centres-vaccination.json"'
        buildConfigField 'String', 'VACCINATION_CENTER_LAST_UPDATE_FILENAME', '"lastUpdate.json"'
        buildConfigField 'String', 'PLAY_STORE_URL', '"market://details?id=fr.gouv.android.stopcovid"'
        buildConfigField 'String', 'APP_GALLERY_URL', '"appmarket://details?id=fr.gouv.android.stopcovid"'
        buildConfigField 'String', 'BONJOUR_WEBSITE_URL', '"https://bonjour.tousanticovid.gouv.fr"'
    }

    lintOptions {
        disable "GradleDependency",
                "IidCompatibilityCheckFailure",
                "InvalidPackage",
                "ObsoleteLintCustomCheck",
                "UnsafeExperimentalUsageError",
                "UnsafeExperimentalUsageWarning",
                "VectorPath",
                "VectorRaster"
    }

    testOptions {
        animationsDisabled true
    }

    buildFeatures.viewBinding = true
}

dependencies {

    implementation "com.github.PhilJay:MPAndroidChart:_"

    implementation "androidx.documentfile:documentfile:_"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:_"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:_"
    implementation "androidx.work:work-runtime-ktx:_"

    implementation "com.airbnb.android:lottie:_"

    implementation("com.journeyapps:zxing-android-embedded:_") { transitive = false }
    implementation "com.google.zxing:core:_" // 3.4 require minSdk 24

    implementation "com.github.razir.progressbutton:progressbutton:_"

    implementation project(path: ':coreui')
    implementation project(path: ':framework')
    implementation project(path: ':robert')
    implementation project(path: ':domain')

    androidTestImplementation "androidx.test.ext:junit:_"
    androidTestImplementation "androidx.test:runner:_"
}
