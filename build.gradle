/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 *
 * Authors
 * ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
 * Created by Lunabee Studio / Date - 2020/04/05 - for the TOUS-ANTI-COVID project
 */

buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter {
            content {
                // https://issuetracker.google.com/issues/179291081 & https://youtrack.jetbrains.com/issue/KT-44730
                includeModule("org.jetbrains.trove4j", "trove4j")
            }
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:_"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:_"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:_"
        classpath "com.google.protobuf:protobuf-gradle-plugin:_"

        // https://issuetracker.google.com/issues/159151549#comment3
        classpath "org.ow2.asm:asm:_"
        classpath "org.ow2.asm:asm-util:_"
        classpath "org.ow2.asm:asm-commons:_"
    }
}

plugins {
    id 'io.gitlab.arturbosch.detekt'
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:_"
}

detekt {
    input = files(rootProject.rootDir)
    config = files("$projectDir/detekt-config.yml")
    baseline = file("$projectDir/detekt-baseline.xml")
    failFast = false
    parallel = true
    disableDefaultRuleSets = true
    buildUponDefaultConfig = true
    autoCorrect = true
    ignoreFailures = true
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt) {
    exclude("**/build/**")
    exclude("**/dgca-app-core-android/**")
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        jcenter {
            content {
                // https://issuetracker.google.com/issues/179291081 & https://youtrack.jetbrains.com/issue/KT-44730
                includeModule("org.jetbrains.trove4j", "trove4j")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
