/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 *
 * Authors
 * ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
 * Created by Lunabee Studio / Date - 2020/04/05 - for the TOUS-ANTI-COVID project
 */

buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter {
            content {
                // https://issuetracker.google.com/issues/179291081 & https://youtrack.jetbrains.com/issue/KT-44730
                includeModule("org.jetbrains.trove4j", "trove4j")
            }
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:_"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:_"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:_"
        classpath "com.google.protobuf:protobuf-gradle-plugin:_"

        // https://issuetracker.google.com/issues/159151549#comment3
        classpath "org.ow2.asm:asm:_"
        classpath "org.ow2.asm:asm-util:_"
        classpath "org.ow2.asm:asm-commons:_"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        jcenter {
            content {
                // https://issuetracker.google.com/issues/179291081 & https://youtrack.jetbrains.com/issue/KT-44730
                includeModule("org.jetbrains.trove4j", "trove4j")
            }
        }
    }
}

subprojects{
    afterEvaluate {
        android {
            compileSdkVersion 30
            buildToolsVersion "30.0.3"

            defaultConfig {
                minSdkVersion 21
                targetSdkVersion 30

                def gitRev = "git rev-parse HEAD".execute().getText().substring(0, 7)
                buildConfigField 'String', 'BUILD_ID', /"${gitRev}"/
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
